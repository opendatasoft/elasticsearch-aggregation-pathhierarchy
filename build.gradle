buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath "org.elasticsearch.gradle:build-tools:${es_version}"
    }
}

// the spotless plugin is dedicated to format the code
plugins {
  id("com.diffplug.spotless") version "7.0.4"
}

repositories {
    mavenLocal()
    mavenCentral()
}

// add -Xlint:deprecation to check the Elasticsearch deprecation warning at compile-time
allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation"
            options.compilerArgs << "-Xlint:unchecked"
        }
    }
}

group = 'org.elasticsearch.plugin'
version = "${plugin_version}"

def versions = org.elasticsearch.gradle.VersionProperties.versions

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'elasticsearch.esplugin'
apply plugin: 'elasticsearch.yaml-rest-test'

// automatic formatting configuration (the same configuration as elasticsearch)
spotless {
    java {
        importOrderFile('config/elastic.importorder') // import order file as exported from elastic
        eclipse().configFile('config/formatterConfig.xml')
        trimTrailingWhitespace()
        target 'src/**/*.java'
    }
}

check.dependsOn spotlessCheck

esplugin {
    name 'pathhierarchy-aggregation'
    description 'Return a path hierarchy aggregation'
    classname 'org.opendatasoft.elasticsearch.plugin.PathHierarchyAggregation'
    licenseFile = rootProject.file('LICENSE')
    noticeFile = rootProject.file('README.md')
}


dependencies {
    implementation "org.elasticsearch:elasticsearch:${es_version}"
    yamlRestTestImplementation "org.elasticsearch.test:framework:${es_version}"
    yamlRestTestImplementation "org.elasticsearch.test:yaml-rest-runner:${es_version}"
    yamlRestTestImplementation "org.apache.logging.log4j:log4j-core:${versions.log4j}"
}

tasks.named("yamlRestTest").configure {
  systemProperty 'tests.security.manager', 'false'
}

tasks.named("test").configure {
  systemProperty 'tests.security.manager', 'false'
}
